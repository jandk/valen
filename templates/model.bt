#include <common/common.bt>

struct ModelHeader {
    Vector3 referencePosition;
    uint numLods;
    uint numSurfaces;
    float maxLodDeviations[numLods];
    uint streamable;
};

struct ModelLodInfo {
    int alwaysMinus2;
    uint numVerts;
    uint numIndices;
    uint flags;
    Bounds bounds;
    Vector3 vertexOffset;
    float vertexScale;
    Vector2 uvOffset;
    float uvScale;
    uint always0;
    float unkFloat1;
    float unkFloat2;
    char alwaysBLMr[4];
};

struct textureAxis_t {
    Mat3 axis;
    Vector3 origin;
    Vector2 scale;
};

struct ModelSettings {
    float lightmapSurfaceAreaSqrt;
    int lighmapWidth;
    int lighmapHeight;
    uint numTextureAxes;
    textureAxis_t textureAxes[numTextureAxes];
};

struct ModelGeoDecalProjection {
    Vector4 projS;
    Vector4 projT;
};

struct ModelGeoDecals {
    uint numGeoDecals;
    ModelGeoDecalProjection projections[numGeoDecals];
    SString declName;
    uint geoDecalTintStartOffset;
};

struct ModelMeshInfo {
    SString declName;
    uint unkHash;
    uint unknown;
    uint always0;

    local uint i;
    for(i = 0; i < 5; i++) {
        uint absent;
        if(absent == 0) {
            ModelLodInfo lodInfo <style=sHeading3>;
        }
    }
};

struct Layout {
    uint numStreams;
    GeometryMemoryLayout memoryLayout[numStreams] <optimize=false>;
    GeometryDiskLayout diskLayout;
};

struct Model {
    ModelHeader header <style=sHeading1>;
    ModelMeshInfo meshInfo[header.numSurfaces] <style=sHeading2, optimize=false>;
    ModelSettings settings <style=sHeading4>;
    ModelGeoDecals geoDecals <style=sHeading1>;
    ubyte streamedSurfaces[header.numLods * header.numSurfaces] <style=sHeading3>;
    if (header.streamable) {
        Layout layouts[5] <style=sHeading4, optimize=false>;
    }
} model;
